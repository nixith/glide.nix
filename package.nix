{
  lib,
  stdenv,
  fetchurl,
  adwaita-icon-theme,
  alsa-lib,
  autoPatchelfHook,
  copyDesktopItems,
  curl,
  dbus-glib,
  gtk3,
  hicolor-icon-theme,
  libXtst,
  libva,
  makeBinaryWrapper,
  makeDesktopItem,
  patchelfUnstable,
  pciutils,
  pipewire,
  wrapGAppsHook3,
  nix-update-script,
  ...
}:
stdenv.mkDerivation (finalAttrs: {
  pname = "glide-browser";
  version = "0.1.54a";

  src =
    let
      sources = {
        "x86_64-linux" = fetchurl {
          url = "https://github.com/glide-browser/glide/releases/download/${finalAttrs.version}/glide.linux-x86_64.tar.xz";
          sha256 = "0scl2v2p6zsgnyq275m9ndiqjnpchnc63a9mncyj6sjyxxpkj3s7";
        };
        "aarch64-linux" = fetchurl {
          url = "https://github.com/glide-browser/glide/releases/download/${finalAttrs.version}/glide.linux-aarch64.tar.xz";
          sha256 = "0qrwdkga6ykxfrkhhzc600j878dy2hd1jyxjyy0wj0w24cs9x1qa";
        };
        "x86_64-darwin" = fetchurl {
          url = "https://github.com/glide-browser/glide/releases/download/${finalAttrs.version}/glide.macos-x86_64.dmg";
          sha256 = "1ivli27cg6sn7qri2yxw7pmqdm63n8mdnsgs1vdw62dy1f0xijfs";
        };
        "aarch64-darwin" = fetchurl {
          url = "https://github.com/glide-browser/glide/releases/download/${finalAttrs.version}/glide.macos-aarch64.dmg";
          sha256 = "1x01hh21zd26fg3hfa0wq8c4avl198jdmwjy0axwpvaj81njrdwq";
        };
      };
    in
    sources.${stdenv.hostPlatform.system};

  nativeBuildInputs = [
    copyDesktopItems
    makeBinaryWrapper
  ] ++ lib.optionals stdenv.isLinux [
    autoPatchelfHook
    patchelfUnstable
    wrapGAppsHook3
  ];

  buildInputs = lib.optionals stdenv.isLinux [
    adwaita-icon-theme
    alsa-lib
    dbus-glib
    gtk3
    hicolor-icon-theme
    libXtst
  ];

  runtimeDependencies = lib.optionals stdenv.isLinux [
    curl
    libva.out
    pciutils
  ];

  appendRunpaths = lib.optionals stdenv.isLinux [ "${pipewire}/lib" ];

  # Firefox uses "relrhack" to manually process relocations from a fixed offset
  patchelfFlags = lib.optionals stdenv.isLinux [ "--no-clobber-old-sections" ];

  unpackPhase = lib.optionalString stdenv.isDarwin ''
    runHook preUnpack
    
    /usr/bin/hdiutil attach -nobrowse -readonly $src
    cp -r /Volumes/Glide/Glide.app .
    /usr/bin/hdiutil detach /Volumes/Glide
    
    runHook postUnpack
  '';

  installPhase = if stdenv.isLinux then ''
    runHook preInstall

    mkdir -p $out/bin $out/share/icons/hicolor/ $out/lib/glide-browser-bin-${finalAttrs.version}
    cp -t $out/lib/glide-browser-bin-${finalAttrs.version} -r *
    chmod +x $out/lib/glide-browser-bin-${finalAttrs.version}/glide
    iconDir=$out/share/icons/hicolor
    browserIcons=$out/lib/glide-browser-bin-${finalAttrs.version}/browser/chrome/icons/default

    for i in 16 32 48 64 128; do
      iconSizeDir="$iconDir/''${i}x$i/apps"
      mkdir -p $iconSizeDir
      cp $browserIcons/default$i.png $iconSizeDir/glide-browser.png
    done

    ln -s $out/lib/glide-browser-bin-${finalAttrs.version}/glide $out/bin/glide
    ln -s $out/bin/glide $out/bin/glide-browser

    runHook postInstall
  '' else ''
    runHook preInstall

    mkdir -p $out/Applications
    cp -r Glide.app $out/Applications/
    
    mkdir -p $out/bin
    ln -s $out/Applications/Glide.app/Contents/MacOS/glide $out/bin/glide
    ln -s $out/bin/glide $out/bin/glide-browser

    runHook postInstall
  '';

  desktopItems = [
    (makeDesktopItem {
      name = "glide-browser-bin";
      exec = "glide-browser --name glide-browser %U";
      icon = "glide-browser";
      desktopName = "Glide Browser";
      genericName = "Web Browser";
      terminal = false;
      startupNotify = true;
      startupWMClass = "glide-browser";
      categories = [
        "Network"
        "WebBrowser"
      ];
      mimeTypes = [
        "text/html"
        "text/xml"
        "application/xhtml+xml"
        "application/vnd.mozilla.xul+xml"
        "x-scheme-handler/http"
        "x-scheme-handler/https"
      ];
      actions = {
        new-window = {
          name = "New Window";
          exec = "glide-browser --new-window %U";
        };
        new-private-window = {
          name = "New Private Window";
          exec = "glide-browser --private-window %U";
        };
        profile-manager-window = {
          name = "Profile Manager";
          exec = "glide-browser --ProfileManager";
        };
      };
    })
  ];

  passthru.updateScript = nix-update-script {
    extraArgs = [
      "--url"
      "https://github.com/glide-browser/glide"
    ];
  };

  meta = {
    changelog = "https://glide-browser.app/changelog#${finalAttrs.version}";
    description = "Extensible and keyboard-focused web browser, based on Firefox (binary package)";
    homepage = "https://glide-browser.app/";
    license = lib.licenses.mpl20;
    sourceProvenance = [ lib.sourceTypes.binaryNativeCode ];
    platforms = [ "x86_64-linux" "aarch64-linux" "x86_64-darwin" "aarch64-darwin" ];
    maintainers = with lib.maintainers; [ pyrox0 ];
    mainProgram = "glide-browser";
  };
})
